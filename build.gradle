plugins {
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id 'org.sonarqube' version '3.0'
	id 'com.github.kt3k.coveralls' version '2.10.2'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.jfrog.artifactory' version '4.15.1'
}

targetCompatibility = 1.8

group = 'io.github.devatherock'
version = '0.1.0' + (Boolean.valueOf(System.getProperty('snapshot')) ? '-SNAPSHOT' : '')

apply from: 'publish.gradle'

repositories {
    jcenter()
}

configurations {
    testCompile.extendsFrom(compileOnly)
}

dependencies {
    compileOnly group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.7'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'
}

jacocoTestReport {
	reports {
		xml.enabled true
        csv.enabled false
        html.enabled true
		xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml") // Required for coveralls
		html.destination file("${buildDir}/reports/jacoco")
	}
}

test {
    testLogging {
        events 'passed', 'failed'
    }
    finalizedBy jacocoTestReport
}

/** SonarQube config **/
sonarqube {
	properties {
		property 'sonar.jacoco.reportPaths', 'build/jacoco/test.exec'
		property 'sonar.junit.reportPaths', 'build/test-results/test'
	}
}